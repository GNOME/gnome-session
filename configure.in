AC_INIT(panel)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-core, 1.0.41)

AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

dnl
dnl let applications configure for gnome
dnl
gnome_cv_use_gnome=yes

GNOME_INIT

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AM_PROG_LEX
AC_PROG_YACC

GNOME_COMPILE_WARNINGS

GNOME_X_CHECKS

AM_PATH_GTK(1.2.5, ,[
AC_MSG_ERROR(Please upgrade your GTK+)])
dnl GNOME_GHTTP_CHECK

dnl utility conditional
AM_CONDITIONAL(FALSE, test "x" = "y")

ALL_LINGUAS="ca cs da de el en_GB es et eu fi fr ga hu it ja ko nl no pl pt ru sk sv wa zh_TW.Big5"
AM_GNU_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

AC_CHECK_HEADERS(dlfcn.h dl.h)
AC_CHECK_LIB(dl, dlopen, DL_LIB="-ldl",[
AC_CHECK_LIB(dld, shl_load, DL_LIB="-ldld",[
AC_CHECK_FUNCS(dlopen, DL_LIB="",
AC_MSG_ERROR(Dynamic linking is not available on this platform.  Some
apps, like panel, will not run properly.))
])])
AC_SUBST(DL_LIB)

dnl properties-keyboard
dnl AC_CHECK_HEADERS(X11/extensions/xf86misc.h, XF86MISC_LIBS="-lXxf86misc")
dnl AC_SUBST(XF86MISC_LIBS)

for dir in `(IFS=:; for i in $PATH; do echo $i ; done)` ; do
  if test -f $dir/orbit-idl ; then
     orbit_prefix=`echo $dir | sed 's%/bin$%%' |sed 's%/bin/$%%'`
     break
  fi
done

ORB_LIBS="$ORBIT_LIBS"
ORB_CFLAGS="$ORBIT_CFLAGS"

AC_SUBST(ORB_LIBS)
AC_SUBST(ORB_CFLAGS)

dnl
dnl Gnome Terminal
dnl
ZVT_LIBS=`$GNOME_CONFIG --libs zvt`
AC_SUBST(ZVT_LIBS)

dnl
dnl Fvwm pager
dnl
dnl AC_CHECK_HEADER(fvwm.h, fvwm=true, fvwm=false)
dnl AM_CONDITIONAL(HAVE_FVWM, $fvwm)

dnl
dnl gnome-session
dnl
dnl $GNOME_HAVE_SM comes from GNOME_X_CHECKS
AM_CONDITIONAL(SESSION, test "$GNOME_HAVE_SM" = true)
AC_ARG_WITH(window-manager,
[  --with-window-manager=NAME
			   Specify default window manager],[
    WINDOW_MANAGER="$with_window_manager"], [
    dnl icewm is the default because it supports session management
    dnl and other Gnome requirements.
    dnl gnome-wm is a shell script that starts the WM.
    WINDOW_MANAGER=gnome-wm])
AC_SUBST(WINDOW_MANAGER)

dnl Check for libgtop
GNOME_LIBGTOP_HOOK(,applets_libgtop=yes)

if test x$applets_libgtop != xyes; then
  AC_MSG_WARN([*** \`cpuload' applet will not be built ***])
  AC_MSG_WARN([*** \`cpumemusage' applet will not be built ***])
  AC_MSG_WARN([*** \`diskusage' applet will not be built ***])
  AC_MSG_WARN([*** \`multiload' applet will not be built ***])
  AC_MSG_WARN([*** \`netload' applet will not be built ***])
fi

dnl Check for ghttp library
AM_CONDITIONAL(HAVE_LIBGHTTP, test -n "$GHTTP_LIB")
AC_SUBST(GHTTP_LIB)
dnl if test -z "$GHTTP_LIB"; then
dnl   AC_MSG_WARN([*** \`bussign' applet will not be built ***])
dnl fi

dnl panel/cdplayer
AC_CANONICAL_HOST
case "$host" in
*linux*) CDROM_HOST=linux;;
*solaris*) CDROM_HOST=solaris;;
esac
AM_CONDITIONAL(APPLET_CDPLAYER, test -n "$CDROM_HOST")
AC_SUBST(CDROM_HOST)
if test -z "$CDROM_HOST"; then
  AC_MSG_WARN([*** \`cdplayer' applet will not be built ***])
fi

dnl panel/mixer
P_MIXER=
AC_CHECK_HEADERS(linux/soundcard.h,[P_MIXER=mixer],[
  AC_CHECK_HEADERS(machine/soundcard.h, [P_MIXER=mixer],[
    AC_CHECK_HEADERS(sys/soundcard.h, [P_MIXER=mixer]) ]) ])
AM_CONDITIONAL(APPLET_MIXER, test "$P_MIXER" = "mixer")
if test "$P_MIXER" != "mixer"; then
  AC_MSG_WARN([*** \`mixer' applet will not be built ***])
fi

dnl panel/modemlights
P_MODEMLIGHTS=
AC_CHECK_HEADERS(net/ppp_defs.h,[P_MODEMLIGHTS=modemlights])
AM_CONDITIONAL(APPLET_MODEMLIGHTS, test "$P_MODEMLIGHTS" = "modemlights")
if test "$P_MODEMLIGHTS" != "modemlights"; then
  AC_MSG_WARN([*** \`modemlights' applet will not be built ***])
fi

dnl esd-manager
dnl have_libesd=no
dnl AM_PATH_ESD(,have_libesd=yes,
dnl [AC_MSG_WARN([*** \`esd-manager' will not be built ***])])
dnl AM_CONDITIONAL(HAVE_LIBESD, test yes = $have_libesd)

dnl help-browser
dnl I found `inet_aton' in -lresolv on solaris
oLIBS="$LIBS"
RES_LIBS=
# the following two are covered by X11 too.  If inet_aton is found in
# plain libc, -lsocket or -lnsl, RES_LIBS will be empty.
AC_CHECK_FUNC(connect,,[AC_CHECK_LIB(socket,connect)])
AC_CHECK_FUNC(gethostbyname,,[AC_CHECK_LIB(nsl,gethostbyname)])
AC_CHECK_FUNC(inet_aton,,[AC_CHECK_LIB(resolv,inet_aton,RES_LIBS="-lresolv")])
AC_SUBST(RES_LIBS)
LIBS="$oLIBS"

dnl
dnl Check for zvt_term_reset
dnl
oLIBS="$LIBS"
LIBS="$GNOMEUI_LIBS $GNOME_LIBDIR $ZVT_LIBS"
AC_CHECK_FUNC(zvt_term_reset,[AC_DEFINE(HAVE_ZVT_TERM_RESET)])
LIBS="$oLIBS"

dnl
dnl Place to look for KDE menus
dnl
AC_ARG_WITH(kde-datadir, [  --with-kde-datadir=DIR         Location of KDE data])

if test "x$with_kde_datadir" = x ; then
  kde_datadir=/usr/share
else
  kde_datadir=$with_kde_datadir
fi

AC_DEFINE_UNQUOTED(KDE_MENUDIR, "$kde_datadir/applnk")
AC_DEFINE_UNQUOTED(KDE_ICONDIR, "$kde_datadir/icons")
AC_DEFINE_UNQUOTED(KDE_MINI_ICONDIR, "$kde_datadir/icons/mini")

dnl AC_CONFIG_SUBDIRS(gemvt)

AC_OUTPUT([
Makefile
gnome-core.spec
po/Makefile.in
macros/Makefile
panel/Makefile
applets/Makefile
applets/applet-dirs/Makefile
applets/asclock/Makefile
applets/asclock/default_theme/Makefile
applets/asclock/themes/Makefile
applets/asclock/themes/Orb/Makefile
applets/asclock/themes/Freeamp/Makefile
applets/asclock/themes/shaped/Makefile
applets/asclock/themes/classic/Makefile
applets/asclock/themes/beats/Makefile
applets/asclock/themes/Stone/Makefile
applets/asclock/themes/Newstone/Makefile
applets/gen_util/Makefile
applets/cdplayer/Makefile
applets/mixer/Makefile
applets/modemlights/Makefile
applets/multiload/Makefile
applets/netload/Makefile
applets/cpumemusage/Makefile
applets/fish/Makefile
applets/diskusage/Makefile
applets/desk-guide/Makefile
applets/icewm-pager/Makefile
applets/gnome-pager/Makefile
applets/esd-manager/Makefile
applets/drivemount/Makefile
applets/clockmail/Makefile
applets/webcontrol/Makefile
applets/dialer/Makefile
applets/fifteen/Makefile
applets/life/Makefile
applets/battery/Makefile
applets/charpick/Makefile
applets/winlist/Makefile
applets/fvwm-pager/Makefile
applets/slashapp/Makefile
applets/mini-commander/Makefile
applets/mini-commander/docs/Makefile
applets/mini-commander/src/Makefile
applets/gkb/Makefile
applets/gkb/gkb/Makefile
applets/gkb/xmodmap/Makefile
applets/jbc/Makefile
applets/jbc/pixmaps/Makefile
applets/another_clock/Makefile
applets/tasklist/Makefile
core-docs/Makefile
desktop-links/Makefile
gnome-terminal/Makefile
gnome-terminal/C/Makefile
gnome-terminal/no/Makefile
help-browser/Makefile
help-browser/gnome-man2html/Makefile
help-browser/gnome-info2html/Makefile
help-browser/help/Makefile
help-browser/help/C/Makefile
help-browser/help/ca/Makefile
help-browser/help/es/Makefile
help-browser/help/et/Makefile
help-browser/help/eu/Makefile
help-browser/help/fr/Makefile
help-browser/help/hu/Makefile
help-browser/help/it/Makefile
help-browser/help/ja/Makefile
help-browser/help/ko/Makefile
help-browser/help/no/Makefile
help-browser/help/wa/Makefile
gmenu/Makefile
gnome-edit/Makefile
idl/Makefile
pixmaps/Makefile
gsm/Makefile
smproxy/Makefile
intl/Makefile])
